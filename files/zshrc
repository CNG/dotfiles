# .zshenv is always sourced, while .zshrc is only sourced in interactive shells

# Put private interactive shell definitions in ~/.zshrc.local
# so they are ignored by Git and not inadvertently made public
if [[ -a ~/.zshrc.local ]]; then
  source ~/.zshrc.local
fi

# Load main configuration file
source $HOME/.zsh/config.zsh

# Gather all config files in dictionary-like array
typeset -U config_files # keep only one occurrence of each value
config_files=($HOME/.zsh/config/***/*.zsh) # *** recurses, follows symlinks

# Load the Oh-My-Zsh plugin files
for file in ${(M)config_files:#*/plugins*}; do
  source $file
done

# Load all but env, completion and plugin files
for file in ${${${config_files:#*/env*}:#*/completion*}:#*/plugins*}; do
  source $file
done

# Load Oh-My-Zsh configuration file 
source $HOME/.zsh/oh-my-zsh.zsh

# Initialize autocomplete here so functions will load
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion*}; do
  source $file
done

unset config_files
