#!/usr/bin/env zsh
# zsh so we can access autoloaded functions

# sync-private-data
#
# This script makes various updates to system files and backups in a Git repo

# Exit if any command returns nonzero or unset variable referenced.
set -o errexit -o pipefail -o nounset
set -x

main () {
  # Next line assumes $DOTFILES/private is Git repository for private files
  local private_repo="$DOTFILES/private"
  local note=${1:-}

  # format optional note to include in commit message
  [[ -z $note ]] || note=" [$note]"

  (
    cd "$private_repo"
    git pull || return

    # Next line assumes Shell history file is ~/.zsh_history
    sync_zsh_history

    # Disabling /etc/hosts customization till decide about Ansible tasks
    # Next line assumes current user can sudo without password
    # update-hosts # Zsh autoloaded function

    # sync_crontab contains a Zsh autoloaded function
    sync_crontab
    # Next line assumes current user can sudo without password
    # sync_crontab root

    git push
  )
}

logger () {
  echo "$@" | systemd-cat -t $(basename $0)
}

sync_crontab () {
  local user=${1:-$(whoami)}
  # not working for some reason, load file directly
  # sync-crontab $user # Zsh autoloaded function
  ~/.zsh/config/functions/sync-crontab $user
  commit_if_untracked crontab_$(hostname)_$user "Initial $user crontab"
  commit_if_modified  crontab_$(hostname)_$user "Update $user crontab"
}


# Usage: commit_if_* <file or dir> <msg>
commit_if_untracked () {
  # [[ $(git ls-files --exclude-standard -o) =~ ($'\n'|^)$1($'\n'|$) ]] || return 0
  [[ -n $(git ls-files --exclude-standard -o $1) ]] || return 0
  git add -A "$1" && git commit -m "$2$note"
}
commit_if_untracked_or_modified () {
  [[ -n $(git ls-files --exclude-standard -om $1) ]] || return 0
  git add -A "$1" && git commit -m "$2$note"
}
commit_if_modified () {
  [[ -n $(git ls-files --exclude-standard -m $1) ]] || return 0
  git add -A "$1" && git commit -m "$2$note"
}


# merge local Zsh history if recently modified
# Globals: $note
sync_zsh_history () {
  local merged=zsh_history
  local splith=${merged}_$(hostname)
  local localh=$HOME/.$merged

  # proceed only if new last line in repo or local
  # possible false match if last command multiline (so update next time, ok)
  local last_merged=$(tail -n 1 "$merged")
  local last_localh=$(tail -n 1 "$localh")

  grep -Fq "$last_localh" "$splith" && grep -Fq "$last_merged" "$localh" &&
    return 0

  # forgot why this is here
  touch $merged $splith

  # add new content (appears locally but not in repo) to host log in repo
  command diff --changed-group-format="%>" --unchanged-group-format="" \
    $merged "$localh" >> $splith || true

  # add local history to combined hosts repo history
  # sort (timestamp prepended lines) and remove duplicates
  command cat "$localh" >> "$merged"
  clean_zsh_history "$merged"

  # commit and copy newly merged history to local Zsh history
  git add $merged $splith
  git commit -m "$(hostname) $merged$note"
  cmp --silent $merged "$localh" || cp -a $merged "$localh"
}


main "$@"
